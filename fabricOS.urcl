// fabricOS v0.3 - Written on URCL v1.5
// Header
BITS >= 16
MINREG 10
MINHEAP 4096
MINSTACK 16

CAL .func_init
CAL .func_shell

// Variables
.var_exit
DW 0
// 0 planned exit
// 1 unexpected error
// 2 unplanned exit
// 3 No Storage File
.var_error
DW 0
// 0 default
// 1 invalid or unimplemented instruction
.var_cwd
DW 0
.var_string1
DW 0
.var_string2
DW 0
.var_string3
DW 0
.var_string4
DW 0

// Virtual Registers
.vr
DW 0 //r0 - Always reads 0
DW 0 //r1
DW 0 //r2
DW 0 //r3
DW 0 //r4
DW 0 //r5
DW 0 //r6
DW 0 //r7
DW 0 //r8
DW 0 //r9
DW 0 //rA
DW 0 //rB
DW 0 //rC
DW 0 //rD
DW 0 //rE
.vr_stack
DW 0 //rF

// Instructions
.instr_f
DW .instr_nop //0x00
DW .instr_sys//.instr_sys //0x01
DW .instr_imm//.instr_imm //0x02
DW .instr_err//.instr_add //0x03
DW .instr_err//.instr_and // 0x04
DW .instr_err//.instr_nor // 0x05
DW .instr_err//.instr_shl// 0x06
DW .instr_err//.instr_shr // 0x07
DW .instr_err//.instr_brg // 0x08
DW .instr_err//.instr_bre // 0x09
DW .instr_err//.instr_psh // 0x0A
DW .instr_err//.instr_pop // 0x0B
DW .instr_err//.instr_lod // 0x0C
DW .instr_err//.instr_str// 0x0D
DW .instr_err//.instr_brc // 0x0E
DW .instr_err//.instr_blk // 0x0F
DW .instr_hlt // 0x10
.instr
BRE .instr_nop R5 0x00
BRE .instr_sys R5 0x01
BRE .instr_imm R5 0x02
BRE .instr_add R5 0x03
BRE .instr_hlt R5 0x10
JMP .instr_err

.instr_nop
  RET

.instr_sys
  BRE .sys_exit R2 0x0000
  BRE .sys_numb R2 0x0400
  BRE .sys_newl R2 0x0500
  JMP .instr_err

.instr_imm
  ADD R2 .vr R2
  STR R2 R3
  RET 

.instr_add
  ADD R3 .vr R3
  LOD R3 R3
  ADD R4 .vr R4
  LOD R4 R4
  ADD R1 R3 R4
  ADD R2 .vr R2
  STR R2 R1
  RET 

.instr_and
  ADD R3 .vr R3
  LOD R3 R3
  ADD R4 .vr R4
  LOD R4 R4
  AND R1 R3 R4
  ADD R2 .vr R2
  STR R2 R1
  RET 

.instr_nor
  ADD R3 .vr R3
  LOD R3 R3
  ADD R4 .vr R4
  LOD R4 R4
  NOR R1 R3 R4
  ADD R2 .vr R2
  STR R2 R1
  RET 

.instr_err
  IMM R1 .str_invalidinstr
  CAL .func_pstring
  STR .var_exit 1
  STR .var_error 1
  JMP .instr_hlt

.instr_hlt
  IMM R1 .str_exit
  CAL .func_pstring
  LOD R3 .var_exit
  OUT %NUMB R3
  HLT

// Syscalls
.sys
.sys_exit
  IMM R1 .str_exit
  CAL .func_pstring
  STR .var_exit R2
  LOD R2 .var_exit
  OUT %NUMB R2
  HLT

.sys_numb
  BRE .sys_numb_in R3 0x01
  ADD R4 .vr R4
  LOD R4 R4
  OUT %NUMB R4
  RET
  .sys_numb_in
  JMP .instr_err

.sys_newl
  OUT %TEXT '\n'
  RET

.sys_fill
  BRE .sys_fill_test R3 0x01
  IN R1 %x
  IN R2 %y
  IMM R3 0
  IMM R5 0
  OUT %X R3
  OUT %Y R5
  .sys_fill_loop
  OUT %X R3
  OUT %COLOR R4
  BRE .sys_fill_row R3 R1
  INC R3 R3
  OUT %X R3
  OUT %Y R5
  JMP .sys_fill_loop
  .sys_fill_row
  IMM R3 0
  BRE .func_ret R5 R2
  INC R5 R5
  JMP .sys_fill_loop

.sys_fill_test
  IN R1 %x
  IN R2 %y
  IMM R3 0
  IMM R5 0
  OUT %X R3
  OUT %Y R5
  .sys_fill_test_loop
  OUT %X R3
  OUT %COLOR R3
  BRE .sys_fill_test_row R3 R1
  INC R3 R3
  OUT %X R3
  OUT %Y R5
  JMP .sys_fill_test_loop
  .sys_fill_test_row
  IMM R3 0
  BRE .func_ret R5 R2
  INC R5 R5
  JMP .sys_fill_test_loop

.sys_grpx
  BRE .sys_grpx_in R3 1
  ADD R4 .vr R4
  LOD R4 R4
  OUT %X R4
  RET
  .sys_grpx_in

// Strings
.str_init
DW [ "System > Initializing...\n" 0]
.str_startup
DW [ "System > Welcome to fabricOS! \n" 0]
.str_exit
DW [ "System > Exited with Status " 0]
.str_storagefail
DW [ "System > No Storage File was found. Did you forget to download one?\n" 0]
.str_invalidinstr
DW [ "System > Invalid Instruction\n" 0]
.str_out
DW [ " > " 0]

// Functions
.func_init
  CAL .sys_fill_test
  IMM R1 .str_init
  CAL .func_pstring
  STR .vr_stack SP
  STR M0 0
  STR .var_error 0
  STR .var_exit 2
  STR .var_cwd 1
  OUT %SUPPORTED %BUS
  IN R5 %SUPPORTED
  BRE .func_storagefail R5 0
  IMM R1 .str_startup
  CAL .func_pstring
  RET

.func_pstring //r1 = string address
  LOD R2 R1
  BRZ .func_ret R2
  OUT %TEXT R2
  INC R1 R1
  JMP .func_pstring

.func_shell
  BNZ .func_shell_input_end R4
  IMM r4 0
  OUT %TEXT '/'
  IMM R1 .str_out
  CAL .func_pstring
  JMP .func_shell_input
  .func_shell_input_end
  IMM R4 1
  .func_shell_input
  IN R1 %TEXT
  OUT %TEXT R1
  PSH R1
  INC R3 R3
  BNE .func_shell_input R1 0xA
  STR .var_string1 0
  STR .var_string2 0
  STR .var_string3 0
  STR .var_string4 0
  POP R0
  DEC r3 r3
  BRE .func_shell_input_3 R3 3
  BRE .func_shell_input_3 R3 2
  BRE .func_shell_input_3 R3 1
  BRE .func_shell R3 0
  .func_shell_input_4
  POP R1
  BRE .func_findfile R1 0x20
  STR .var_string4 R1
  .func_shell_input_3
  POP R1
  BRE .func_findfile R1 0x20
  STR .var_string3 R1
  .func_shell_input_2
  POP R1
  BRE .func_findfile R1 0x20
  STR .var_string2 R1
  .func_shell_input_1
  POP R1
  BRE .func_findfile R1 0x20
  STR .var_string1 R1

.func_findfile
  OUT %ADDR R7
  IN R6 %BUS
  AND R5 R6 0xFF00
  BRE .func_findfile_locate R5 0xFF00
  INC R7 R7
  JMP .func_findfile

  .func_findfile_locate
  BRE .func_findfile_inc R6 0xFFFE
  AND R5 R6 0x00FF
  INC R7 R7
  OUT %ADDR R7
  IN R6 %BUS
  MOV R4 R6
  INC R7 R7
  OUT %ADDR R7
  IN R6 %BUS
  BNE .func_findfile R6 0xFFFE
  INC R7 R7
  OUT %ADDR R7
  IN R6 %BUS
  LOD R8 .var_cwd
  BRE .func_findfile_checkname R8 R6
  JMP .func_findfile_locate_continue

  .func_findfile_locate_continue
  INC R7 R7
  OUT %ADDR R7
  IN R6 %BUS
  BNE .func_findfile R6 0xFFFE
  INC R7 R7
  OUT %ADDR R7
  IN R6 %BUS
  LOD R8 .var_cwd
  BNE .func_findfile_locate_continue R6 R8
  JMP .func_findfile_checkname

  .func_findfile_inc
  INC R7 R7
  JMP .func_findfile

.func_findfile_checkname
  INC R7 R7
  OUT %ADDR R7
  IN R6 %BUS
  BRE .func_findfile R6 0xFFFE
  BRE .func_findfile_found 0 R3
  LOD R1 .var_string1
  AND R2 R6 0xFF00
  BSR R2 R2 8
  BNE .func_findfile R2 R1
  BRE .func_findfile_found 1 R3
  LOD R1 .var_string2
  AND R2 R6 0x00FF
  BNE .func_findfile R2 R1
  INC R7 R7
  OUT %ADDR R7
  IN R6 %BUS
  BRE .func_findfile_found 2 R3
  LOD R1 .var_string3
  AND R2 R6 0xFF00
  BSR R2 R2 8
  BNE .func_findfile R2 R1
  BRE .func_findfile_found 3 R3
  LOD R1 .var_string4
  AND R2 R6 0x00FF
  BNE .func_findfile R2 R1
  JMP .func_findfile_found

.func_findfile_found
  INC R7 R7
  OUT %ADDR R7
  IN R6 %BUS
  MOV R1 R6
  BRE .func_findfile_dir R1 0
  JMP .func_findfile_exec
  
.func_findfile_dir
  STR .var_cwd R5
  IMM R4 0
  JMP .func_shell

.func_findfile_exec
  OUT %ADDR R7
  IN R6 %BUS
  BRE .func_exec_check R6 0xFFFF
  INC R7 R7
  JMP .func_findfile_exec

.func_exec_check
  INC R7 R7
  OUT %ADDR R7
  IN R6 %BUS
  BRE .func_findfile_exec R6 0xFFFF
  BNE .func_findfile_exec R6 R1
  INC R7 R7
  JMP .func_exec

.func_exec
BRE .func_exec_switch R10 16
OUT %ADDR R7
IN R6 %BUS
BRE .func_shell R6 0xFF00
AND R2 R6 0xFF00
BSR R2 R2 8
AND R3 R6 0x00FF
INC R7 R7
OUT %ADDR R7
IN R6 %BUS
AND R4 R6 0xFF00
BSR R4 R4 8
AND R5 R6 0x00FF
CAL .instr
INC R7 R7
INC R10 R10
JMP .func_exec

.func_exec_switch
  IMM R10 0
  INC R7 R7
  OUT %ADDR R7
  IN R6 %BUS
  MOV R1 R6
  IMM R7 0
  JMP .func_findfile_exec

.func_storagefail
  IMM R1 .str_storagefail
  CAL .func_pstring
  STR .var_exit 3
  JMP .instr_hlt

.func_ret
  RET

